// ==========================================================================
// Rules
// ==========================================================================

// root
path / {
  read() { isSigned() && isAdmin() }
  write() { isSigned() && isAdmin() }
}

// categories
path /categories {
  read() { isSigned() }
  write() { isSigned() && isAdmin() }
}
path /categories/{categoryId} is Category {
  write() { isSigned() && isAdmin()}
}

// categoryKeys
path /categoryKeys {
  read() { isSigned() }
  write() { isSigned() && (isAdmin() || isEditor()) }
}

// posts
path /posts {
  read() { isSigned() }
  write() { isSigned() && (isAdmin() || isEditor()) }
}
path /posts/{postId} is Post {
  write() { isSigned() && isPostAuthor(postId) }
}


// postContents
path /postContents {
  read() { isSigned() }
  write() { isSigned() && (isAdmin() || isEditor()) }
}
path /postContents/{postId} is PostContent;

// team
path /team {
  read() { isSigned() && isTeamMember() }
}
path /team/{userId} is TeamMember {
  index() {["disabled"] }
  write() { isSigned() && isAdmin() }
}


// ==========================================================================
// Types
// ==========================================================================
type Status extends String {
  validate() { this === 'published' || this === 'draft' || this === 'hidden'}
}
type Role extends String {
  validate() { this === 'admin' || this === 'editor' || this === 'author'}
}

type Post {
  title: String,
  subtitle: String | Null,
  author: String,
  status: Status,
  category: String,
  publishTime: Number,
}

type PostContent {
  content: Object
}

type Category {
  name: String
}

type TeamMember {
  disabled: Boolean
}

// ==========================================================================
// Helper functions
// ==========================================================================

isSigned() {
  auth != null
}
isAdmin() {
  auth.token.role === 'admin'
}
isEditor() {
  auth.token.role === 'editor'
}
isAuthor() {
  auth.token.role === 'author'
}
isTeamMember() {
  auth.token.role != null
}
isPostAuthor(postId) {
  root.child('posts/' + postId + '/author').val() === auth.uid
}
