// ==========================================================================
// Rules
// ==========================================================================

// root
path / {
  read() { isSigned() && isAdmin() }
  write() { isSigned() && isAdmin() }
}

// categories
path /categories {
  read() { isSigned() }
  write() { isSigned() && isAdmin() }
}
path /categories/{categoryId} is Category {
  write() { isSigned() && isAdmin()}
}

// categoryKeys
path /categoryKeys {
  read() { isSigned() }
  write() { isSigned() && (isAdmin() || isEditor()) }
}

// posts
path /posts {
  read() { isSigned() }
  write() { isSigned() && (isAdmin() || isEditor()) }
}
path /posts/{postId} is Post {
  write() { isSigned() && isPostAuthor(postId) }
}


// postContents
path /postContents {
  read() { isSigned() }
  write() { isSigned() && (isAdmin() || isEditor()) }
}
path /postContents/{postId} is String;

// polls
path /polls {
  read() { isSigned() }
  write() {
    isSigned() && (isAdmin() || isEditor())
  }
}
path /polls/{postId} is Poll {
  write() { isSigned() && isPostAuthor(postId) }
}

// team
path /team {
  read() { isSigned() && isTeamMember() }
}
path /team/{userId} is TeamMember {
  index() {["disabled"] }
  write() { isSigned() && isAdmin() }
}

//users
path /users/{userId} is User {
  read() { isSigned() && userId === auth.uid}
  write() { isSigned() && userId === auth.uid }
}




// ==========================================================================
// Types
// ==========================================================================
type Status extends String {
  validate() { this === 'published' || this === 'draft' || this === 'hidden'}
}
type Role extends String {
  validate() { this === 'admin' || this === 'editor' || this === 'author'}
}

type Post {
  title: String,
  subtitle: String | Null,
  author: String,
  status: Status,
  category: String,
  publishTime: Number | Null,
}

// type PostContent {
//   content: Object
// }

type Poll {
  question: String,
  options: PollOption[],
}
type PollOption {
  value: String,
  voteCount: Number
}

type User {
  pollVotes: Map<String, Number>
}

type Category {
  name: String
}

type TeamMember {
  disabled: Boolean
}

// ==========================================================================
// Helper functions
// ==========================================================================

isSigned() {
  auth != null
}
isAdmin() {
  auth.token.role === 'admin'
}
isEditor() {
  auth.token.role === 'editor'
}
isAuthor() {
  auth.token.role === 'author'
}
isTeamMember() {
  auth.token.role != null
}
isPostAuthor(postId) {
  root.child('posts/' + postId + '/author').val() === auth.uid
}
